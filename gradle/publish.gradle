/*
 * Copyright 2018 Russell Wolf
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "russhwolf"
            name "Russell Wolf"
        }
    }
    scm {
        url "https://github.com/russhwolf/multiplatform-settings"
    }
}

bintray {
    def keyProps = new Properties()
    def keyFile = file("../keys.properties")
    if (keyFile.exists()) keyFile.withInputStream { keyProps.load(it) }

    user = keyProps.getProperty("bintrayUser")
    key = keyProps.getProperty("bintrayKey")

    pkg {
        userOrg = 'russhwolf'
        repo = 'multiplatform-settings'
        name = "multiplatform-settings"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/russhwolf/multiplatform-settings'
    }
}

afterEvaluate {
    project.publishing.publications.forEach { publication ->
        publication.pom.withXml {
            def root = asNode()
            root.appendNode('name', project.name)
            root.appendNode('description', 'Persisting settings on Android and iOS')
            root.appendNode('url', 'https://github.com/russhwolf/multiplatform-settings')
            root.children().last() + pomConfig
        }
    }
}

bintrayUpload.doFirst {
    publications = project.publishing.publications
}
